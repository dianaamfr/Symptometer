
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX doid: <http://purl.obolibrary.org/obo/>

# Search diseases by symptom
SELECT DISTINCT ?diseaseName ?diseaseID
WHERE {
  ?diseaseID rdfs:label ?diseaseName .
  ?diseaseID rdfs:subClassOf ?restriction .
  ?restriction owl:onProperty doid:RO_0002452 . # R0_0002452 identifies the "has symptom" relation between a disease and a symptom
  ?restriction owl:someValuesFrom ?symptomID .
  ?symptomID rdfs:label "headache" . # symptom label
}

# Search diseases by multiple symptoms - OR operator
SELECT DISTINCT ?diseaseName ?diseaseID
WHERE {
  ?diseaseID rdfs:label ?diseaseName .
  ?diseaseID rdfs:subClassOf ?restriction .
  ?restriction owl:onProperty doid:RO_0002452 .
  ?restriction owl:someValuesFrom ?symptomID .
  ?symptomID rdfs:label ?symptoms . # symptom label
}
VALUES ?symptoms {"chest pain" "headache"}

# Search diseases by multiple symptoms - ordered by number of most symptoms matched
SELECT ?diseaseName (COUNT(?symptomID) AS ?cnt)
WHERE {
  ?diseaseID rdfs:label ?diseaseName .
  ?diseaseID rdfs:subClassOf ?restriction .
  ?restriction owl:onProperty doid:RO_0002452 .
  ?restriction owl:someValuesFrom ?symptomID .
  ?symptomID rdfs:label ?symptoms . # symptom label
  VALUES ?symptoms {"chest pain" "headache" "fever"}
} GROUP BY ?diseaseName
ORDER BY DESC(COUNT(?symptomID))

# Get disease details
# GROUP_CONCAT
SELECT DISTINCT ?symptomName
WHERE{
  ?diseaseID rdfs:label "long COVID"@en . # Some diseases require @en, but most don't allow it
  ?diseaseID rdfs:subClassOf ?restriction .
  ?restriction owl:onProperty doid:RO_0002452 .
  ?restriction owl:someValuesFrom ?symptomID .
  ?symptomID rdfs:label ?symptomName
}

# Get diseases by symptoms - with description and icd id
PREFIX doi: <https://doi.org/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX doid: <http://purl.obolibrary.org/obo/>
PREFIX oboinowl: <http://www.geneontology.org/formats/oboinowl#>

SELECT ?diseaseName ?definition ?icd
WHERE {
	  ?diseaseID rdfs:label ?diseaseName . 
    ?diseaseID doid:IAO_0000115 ?definition .
    ?diseaseID oboinowl:hasDbXref ?icd .
    FILTER(STRSTARTS(STR(?icd), "ICD10CM:"))
  	{
    	SELECT ?diseaseID
        WHERE {
          	?diseaseID rdfs:subClassOf ?restriction .
          	?restriction owl:onProperty doid:RO_0002452 .
          	?restriction owl:someValuesFrom ?symptomID .
          	?symptomID rdfs:label ?symptoms .
        	VALUES ?symptoms {"headache" "fatigue"}
    	} GROUP BY ?diseaseID
    	ORDER BY DESC(COUNT(?symptomID))
  }
}


# Find related symptoms 
# symptom1 é dado pelo user
# sugerir symptoms que estão normalmente associados (ocorrem em simultaneo)
SELECT ?symptom
WHERE {
      ?rest owl:onProperty doid:RO_0002452 .
      ?rest2 owl:onProperty doid:RO_0002452 .
      ?rest owl:someValuesFrom ?symptomID1 .
      ?rest2 owl:someValuesFrom ?symptomID2 .
      ?diseaseID rdfs:label ?diseaseName .
      ?diseaseID rdfs:subClassOf ?rest .
      ?diseaseID rdfs:subClassOf ?rest2 .
      ?symptomID1 rdfs:label "headache".
      ?symptomID2 rdfs:label ?symptom.
      FILTER (?symptom != "headache")
}
GROUP BY ?symptom
ORDER BY DESC(COUNT(?symptom))


# Diseases on each body part (brain, kidneys etc)

SELECT ?location (COUNT(?diseaseID) AS ?num_diseases)
WHERE {
  ?diseaseID rdfs:label ?diseaseName .
  ?diseaseID rdfs:subClassOf ?restriction .
  ?restriction owl:onProperty doid:RO_0004026 .
  ?restriction owl:someValuesFrom ?locationID .
  ?locationID rdfs:label ?location . 
}
GROUP BY(?location)
ORDER BY DESC(COUNT(?diseaseID))


# Find to which group a disease belongs to:
SELECT DISTINCT ?groupName (COUNT(?diseaseID) AS ?num_subgroups)
WHERE {
  ?diseaseID rdfs:label "COVID-19"@en.
  ?diseaseID rdfs:subClassOf* ?classID .
  ?classID rdfs:subClassOf doid:DOID_4.
  ?classID rdfs:label ?groupName .
}
GROUP BY ?groupName
ORDER BY DESC(COUNT(?diseaseID))

# Find list of all subgroups that a disease belongs to:
# EX: Covid belongs to "Coronavirus infectious disease"@en < viral infectious disease < disease by infectious agent < disease
SELECT DISTINCT ?groupName
WHERE {
  ?diseaseID rdfs:label "COVID-19"@en.
  ?diseaseID rdfs:subClassOf* ?classID .
  ?classID rdfs:label ?groupName .
}

# List of the eight main groups:

SELECT DISTINCT ?groupName (COUNT(?diseaseID) AS ?num_subgroups)
WHERE {
  ?diseaseID rdfs:label ?diseaseName.
  ?diseaseID rdfs:subClassOf ?classID .
  ?classID rdfs:subClassOf doid:DOID_4.
  ?classID rdfs:label ?groupName .
}
GROUP BY ?groupName
ORDER BY DESC(COUNT(?diseaseID))

# List of all possible groups and number of diseases on each group:

SELECT DISTINCT ?groupName (COUNT(?diseaseID) AS ?num_subgroups)
WHERE {
  ?diseaseID rdfs:label ?diseaseName.
  ?diseaseID rdfs:subClassOf ?classID .
  ?classID rdfs:label ?groupName .
}
GROUP BY ?groupName
ORDER BY DESC(COUNT(?diseaseID))

# List of synonyms for a disease:
SELECT ?exactSynonym
WHERE {
  ?diseaseID oboinowl:id "DOID:10784" .
  ?diseaseID oboinowl:hasExactSynonym ?exactSynonym . 
}

#List of related synonyms for a disease:
SELECT ?hasRelatedSynonym 
WHERE {
  ?diseaseID oboinowl:id "DOID:10784" .
  ?diseaseID oboinowl:hasRelatedSynonym ?hasRelatedSynonym . 
}

# Disease definition
SELECT ?diseaseName ?definition
WHERE {
	?diseaseID rdfs:label ?diseaseName . 
  ?diseaseID doid:IAO_0000115 ?definition .
  ?diseaseID oboinowl:id "DOID:10784" .
}

# Disease group
SELECT ?diseaseName ?groupName
WHERE {
  ?diseaseID rdfs:label ?diseaseName .
  ?diseaseID rdfs:subClassOf ?classID .
  ?classID rdfs:label ?groupName .
  ?diseaseID oboinowl:id "DOID:10784" .
}

#Group of a Disease group
SELECT ?diseaseName ?groupName ?groupGroupName
WHERE {
  ?diseaseID rdfs:label ?diseaseName .
  ?diseaseID rdfs:subClassOf ?classID .
  ?classID rdfs:label ?groupName .
  ?classID rdfs:subClassOf ?groupClassID .
  ?groupClassID rdfs:label ?groupGroupName .
  ?diseaseID oboinowl:id "DOID:10784" .
}

# Disease symptoms
SELECT ?diseaseName ?symptomName
WHERE {
  ?diseaseID rdfs:label ?diseaseName .
  ?diseaseID rdfs:subClassOf ?restriction .
  ?restriction owl:onProperty doid:RO_0002452 .
  ?restriction owl:someValuesFrom ?symptomID .
  ?symptomID rdfs:label ?symptomName .
  ?diseaseID oboinowl:id "DOID:10784" .
}

#Disease name, definition, group and group of group
SELECT ?diseaseName ?definition ?groupName ?groupGroupName
WHERE {
  	?diseaseID oboinowl:id "DOID:10784" .
	  ?diseaseID rdfs:label ?diseaseName . 
    ?diseaseID doid:IAO_0000115 ?definition .
  	?diseaseID rdfs:subClassOf ?classID .
  	?classID rdfs:label ?groupName .
  	?classID rdfs:subClassOf ?groupClassID .
  	?groupClassID rdfs:label ?groupGroupName .
}

#Related Diseases
SELECT ?diseaseName ?relatedDiseaseName
WHERE {
  ?diseaseID rdfs:label ?diseaseName .
  ?diseaseID rdfs:subClassOf ?restriction .
  ?restriction owl:onProperty doid:RO_0004020 .
  ?restriction owl:someValuesFrom ?relatedDiseaseID .
  ?relatedDiseaseID rdfs:label ?relatedDiseaseName .
  ?diseaseID oboinowl:id "DOID:10784" .
}